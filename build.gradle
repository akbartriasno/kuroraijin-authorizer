plugins {
    id("io.micronaut.application") version "4.5.4"
    id("com.gradleup.shadow") version "8.3.7"
    id("io.micronaut.aot") version "4.5.4"
}

version = "0.1"
group = "com.kuroraijin"

repositories {
    mavenCentral()
}

dependencies {
    // --- Micronaut Core ---
    implementation("io.micronaut:micronaut-runtime")
    implementation("io.micronaut:micronaut-inject")
    runtimeOnly("io.micronaut:micronaut-http-client")

    // --- AWS Lambda Function (Custom Runtime) ---
    implementation("io.micronaut.aws:micronaut-function-aws")
    implementation("io.micronaut.aws:micronaut-function-aws-custom-runtime")

    // --- JSON (Serde) ---
    annotationProcessor("io.micronaut.serde:micronaut-serde-processor")
    implementation("io.micronaut.serde:micronaut-serde-jackson")

    // === security (JWT) ===
    implementation("io.micronaut.security:micronaut-security")
    implementation("io.micronaut.security:micronaut-security-jwt")

    // === MongoDB ===
    implementation("io.micronaut.mongodb:micronaut-mongo-sync")
    runtimeOnly("org.mongodb:mongodb-driver-sync")

    // --- Logging ---
    runtimeOnly("ch.qos.logback:logback-classic")

    // --- Optional: CRaC (cold-start optimization) ---
    implementation("io.micronaut.crac:micronaut-crac")

    // --- Test ---
    testImplementation("io.micronaut.test:micronaut-test-junit5")
    testImplementation("org.junit.jupiter:junit-jupiter-api")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
}

application {
    mainClass = "com.kuroraijin.FunctionLambdaRuntime"
}

java {
    sourceCompatibility = JavaVersion.toVersion("21")
    targetCompatibility = JavaVersion.toVersion("21")
}

micronaut {
    runtime("lambda_provided")
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("com.kuroraijin.*")
    }
    aot {
        optimizeServiceLoading = true
        convertYamlToJava = false
        precomputeOperations = true
        cacheEnvironment = true
        optimizeClassLoading = true
        deduceEnvironment = true
        optimizeNetty = true
        replaceLogbackXml = true
    }
}

// Optimize native image build args
graalvmNative {
    binaries {
        main {
            buildArgs.add("--initialize-at-build-time=org.slf4j")
            buildArgs.add("--initialize-at-build-time=ch.qos.logback")
            buildArgs.add("-H:+ReportExceptionStackTraces")
            buildArgs.add("-H:+RemoveUnusedSymbols")
            buildArgs.add("--no-fallback")

            // Memory optimizations for runtime
            buildArgs.add("-R:MaxHeapSize=512m")
            buildArgs.add("-R:MinHeapSize=256m")
            buildArgs.add("-R:MaxNewSize=256m")
        }
    }
}

tasks.named("dockerfileNative") {
    jdkVersion = "21"
    args(
            "-XX:MaximumHeapSizePercent=80",
            "-Dio.netty.allocator.numDirectArenas=0",
            "-Dio.netty.noPreferDirect=true"
    )
}

tasks.register('packageLambda', Zip) {
    dependsOn tasks.named('nativeCompile')

    def nativeDir = layout.buildDirectory.dir('native/nativeCompile')

    from(nativeDir) {
        include 'kuroraijin-authorizer'
        rename { 'bootstrap' }
        filePermissions {
            unix('755')
        }
    }

    destinationDirectory = nativeDir.get().asFile
    archiveFileName = 'function.zip'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}